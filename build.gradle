plugins {
	id 'groovy'
	id 'java-gradle-plugin'
    id 'codenarc'
    id 'maven-publish'
}

version = '0.0.0'
group = 'ru.kinca.gradle'

repositories {
	mavenCentral()
}

sourceSets {
    functionalTest {
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}
task functionalTest(type: Test) {
    description = 'Runs the functional tests.'
    group = 'verification'
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    mustRunAfter test
}
check.dependsOn functionalTest

gradlePlugin {
    testSourceSets sourceSets.functionalTest
}

[ 'codenarcFunctionalTest', 'codenarcTest' ].each {
    "$it"() {
        configFile = file('config/codenarc/test.groovy')
    }
}

codenarcMain {
    configFile = file('config/codenarc/main.groovy')
}

gradlePlugin {
    plugins {
        googleDriveUploader {
            id = 'ru.kinca.google-drive-uploader'
            implementationClass = 'ru.kinca.gradle.googledrive.GoogleDriveUploaderPlugin'
        }
    }
}

tasks.withType(GroovyCompile) {
    groovyOptions.optimizationOptions.indy = true
}
compileGroovy {
    groovyOptions.configurationScript = file('config/compiler-customizer.groovy')
}

configurations.all {
    exclude group: 'com.google.guava', module:'guava-jdk5'
}
dependencies {
    def spockDependency = dependencies.create('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }

	compile localGroovy()
	compile gradleApi()
    compile 'com.google.apis:google-api-services-drive:v3-rev80-1.22.0'
    compile 'com.google.api-client:google-api-client:1.22.0'
    compile 'com.google.oauth-client:google-oauth-client-jetty:1.22.0'
    testCompile spockDependency
    testRuntime 'cglib:cglib-nodep:3.2.5'
    testRuntime 'org.objenesis:objenesis:1.3'
    functionalTestCompile spockDependency
    functionalTestCompile gradleTestKit()
}
